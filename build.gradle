plugins {
	id 'java'
	id 'scala'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.metrifuge'
version = '0.0.1-SNAPSHOT'
description = 'Demo project for Spring Boot'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

configurations {
	gatling
}

sourceSets {
	gatling {
		scala {
			srcDirs = ['src/gatling/scala']
		}
		resources {
			srcDirs = ['src/gatling/resources']
		}
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

	// Add these to the gatling sourceSet's implementation configuration
	gatlingImplementation 'io.gatling.highcharts:gatling-charts-highcharts:3.11.5'
	gatlingImplementation 'org.scala-lang:scala-library:2.13.12'

	// Keep in gatling config for runtime
	gatling 'io.gatling.highcharts:gatling-charts-highcharts:3.11.5'
	gatling 'org.scala-lang:scala-library:2.13.12'
}

tasks.named('test') {
	useJUnitPlatform()
}

task gatlingRun(type: JavaExec) {
	description = 'Run Gatling simulation'
	group = 'gatling'

	dependsOn compileGatlingScala

	classpath = sourceSets.gatling.runtimeClasspath + configurations.gatling
	mainClass = 'io.gatling.app.Gatling'

	args = [
			'--simulation', 'LogSimulatorSimulation',
			'--results-folder', "${buildDir}/gatling-results",
			'--binaries-folder', sourceSets.gatling.output.classesDirs.asPath,
			'--resources-folder', sourceSets.gatling.resources.srcDirs.first()
	]
}